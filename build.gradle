plugins {
	id 'java-library'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'maven-publish'
}

group = 'kr.suhsaechan'
version = '0.1.4'
description = 'SUH-AIDER'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-configuration-processor'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// OkHttp
	api 'com.squareup.okhttp3:okhttp:4.12.0'
	implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

	// Jackson (JSON 처리)
	api 'com.fasterxml.jackson.core:jackson-databind'
	api 'com.fasterxml.jackson.core:jackson-annotations'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar { enabled = false }
jar { enabled = true }

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = 'suh-aider'
				description = '서새찬 AI 서버 통신 라이브러리'
				url = 'https://github.com/Cassiiopeia/suh-aider/'
			}
		}
	}
	repositories {
		mavenLocal()
		maven {
			name = 'nexus'
			url = uri(
					version.endsWith('SNAPSHOT')
							? "https://nexus.suhsaechan.kr/repository/maven-snapshots/"
							: "https://nexus.suhsaechan.kr/repository/maven-releases/"
			)
			allowInsecureProtocol = true
			credentials {
				username = project.findProperty('nexusUsername')
				password = project.findProperty('nexusPassword')
			}
		}
	}
}